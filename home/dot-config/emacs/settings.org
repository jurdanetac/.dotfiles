#+TITLE: Personal Emacs configuration
#+STARTUP: content

* LOAD THESE SETTINGS ON YOUR INIT.EL!
Using Org mode to tweak Emacs is a great way to organize and highlight parts
of your custom configuration. Remember to load this org file in your init.el!

#+begin_example
(require 'org)
(org-babel-load-file
 (expand-file-name "settings.org"
                   user-emacs-directory))
#+end_example

* PROFILE EMACS STARTUP
Measure how much does Emacs takes on launch

#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook
          (lambda ()
            (message "*** Emacs loaded in %s with %d garbage collections."
                     (format "%.2f seconds"
                             (float-time
                              (time-subtract after-init-time before-init-time)))
                     gcs-done)))
#+end_src

* SOURCES
MELPA is an ELPA-compatible package repository that contains an enormous
amount of useful Emacs packages. In contrast to ELPA, Emacs is not configured
by default to install packages from MELPA. You will have to configure it.

#+begin_src emacs-lisp
(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
(package-initialize)

;; Update package archives if no past data is found
(unless package-archive-contents
  (package-refresh-contents))
#+end_src

* CLIPBOARD

#+begin_src emacs-lisp
;; disable clipboard integration
(setq x-select-enable-clipboard nil)

(defun paste-from-clipboard ()
  (interactive)
  (setq x-select-enable-clipboard t)
  (yank)
  (setq x-select-enable-clipboard nil))

(defun copy-to-clipboard()
  (interactive)
  (setq x-select-enable-clipboard t)
  (kill-ring-save (region-beginning) (region-end))
  (setq x-select-enable-clipboard nil))
#+end_src

* UI TWEAKS
** Toggle stuff

#+begin_src emacs-lisp
;; Hide interface tools
(setq inhibit-startup-screen t)

(menu-bar-mode -1)
(tool-bar-mode -1)
(scroll-bar-mode -1)

;; Maximize all frames by default, works on emacsclient too
(add-to-list 'default-frame-alist '(fullscreen . maximized))

;; Disable all alarms, sound is annoying and visual bell hangs the screen for a
;; while when top/bottom is reached with mouse scrolling
(setq ring-bell-function 'ignore)

;; Don't consider case significant for buffer/file-name completion
(setq read-file-name-completion-ignore-case t)
(setq read-buffer-completion-ignore-case t)

;; Prevent emacs from contaminating system's clipboard
(setq select-enable-clipboard nil)
;; Use primary selection to paste (last stretch of text selected in X)
(setq select-enable-primary t)
#+end_src

** Buffers, lines, characters and whitespace

#+begin_src emacs-lisp
;; Avoid surprises with the coding system
(set-default-coding-systems 'utf-8)

;; Display line numbers
(global-display-line-numbers-mode 1)
;; Non-nil means do not display continuation lines.
;; Instead, give each line of text just one screen line
(setq-default truncate-lines t)
;; Toggle for line-truncating
(global-set-key (kbd "C-c t") 'toggle-truncate-lines)

;; Display matching pairs of ()[]{} without delay
(setq show-paren-delay 0)
(show-paren-mode 1)

;; Revert Dired and other buffers
(setq global-auto-revert-non-file-buffers t)

;; Revert buffers when the underlying file has changed
(global-auto-revert-mode 1)

;; Use spaces instead of tabs
(setq-default indent-tabs-mode nil)

;; Highlight whitespace and after-80 columns,
;; I like the MSWord-like approach of the pilcrow (¶) toggle formatting marks
(setq whitespace-style '(face trailing space-mark tab-mark lines-tail))
(global-whitespace-mode t)

;; Delete trailing whitespace on save
(add-hook 'before-save-hook
          'delete-trailing-whitespace)

;; Replace list-buffers with the more advanced ibuffer
(global-set-key (kbd "C-x C-b") 'ibuffer)

;; Web programming indent setup
(add-hook 'html-mode-hook (lambda () (setq-local sgml-basic-offset 2)))
(add-hook 'css-mode-hook (lambda () (setq-local css-indent-offset 2)))
(add-hook 'js-mode-hook (lambda () (setq-local js-indent-level 2)))

(defun kill-all-buffers ()
  "Kill all buffers"
  (interactive)
  (if (yes-or-no-p "Do you really want to kill all buffers? ")
      (mapc 'kill-buffer (buffer-list))))

(defun kill-other-buffers ()
  "Kill all other buffers"
  (interactive)
  (if (yes-or-no-p "Do you really want to kill all other buffers? ")
      (mapc 'kill-buffer (delq (current-buffer) (buffer-list)))))

(global-set-key (kbd "C-c a") 'kill-all-buffers)
(global-set-key (kbd "C-c o") 'kill-other-buffers)
#+end_src

** Font face

#+begin_src emacs-lisp
(set-frame-font "JetBrains Mono-13" nil t)

;; Makes commented text and keywords italics.
;; Your font must have an italic face available.
(set-face-attribute 'font-lock-comment-face nil
  :slant 'italic)
(set-face-attribute 'font-lock-keyword-face nil
  :slant 'italic)

;; Needed if using emacsclient.
(add-to-list 'default-frame-alist '(font . "JetBrains Mono-13"))
#+end_src

** Display shortcuts

#+begin_src emacs-lisp
;; Shortcuts for +/- zooming
(global-set-key (kbd "C-=") 'text-scale-increase)
(global-set-key (kbd "C--") 'text-scale-decrease)

;; Instead of selecting text with control, use evil's visual mode
(global-set-key (kbd "C-<up>") 'enlarge-window)
(global-set-key (kbd "C-<down>") 'shrink-window)
(global-set-key (kbd "C-<left>") 'enlarge-window-horizontally)
(global-set-key (kbd "C-<right>") 'shrink-window-horizontally)
#+end_src

* BACKUP FILES HANDLING

#+begin_src emacs-lisp
(setq backup-directory-alist `(("." . "~/.config/emacs/saves")))
(setq backup-by-copying t)
(setq delete-old-versions t
  kept-new-versions 6
  kept-old-versions 2
  version-control t)
#+end_src

* LINE SWITCHING

#+begin_src emacs-lisp
(defun duplicate-line ()
  (interactive)
  (save-mark-and-excursion
    (beginning-of-line)
    (insert (thing-at-point 'line t))))

(defun move-line-down ()
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines 1))
    (forward-line)
    (move-to-column col)))

(defun move-line-up ()
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines -1))
    (forward-line -1)
    (move-to-column col)))

(global-set-key (kbd "C-S-d") 'duplicate-line)
(global-set-key (kbd "C-S-j") 'move-line-down)
(global-set-key (kbd "C-S-k") 'move-line-up)
#+end_src

* PACKAGES
** Use-package
Use-package declaration for simplifying your .emacs

#+begin_src emacs-lisp
(unless (package-installed-p 'use-package)
  (package-install 'use-package))

;; This is only needed once, near the top of the file
(eval-when-compile
  (require 'use-package))

;; Ensure all packages are installed on launch
(setq use-package-always-ensure t)
#+end_src

** Evil mode
The extensible vi layer for Emacs and it's "dependencies"

#+begin_src emacs-lisp
;; Remap universal-argument to allow Evil's `C-u`
(global-set-key (kbd "C-c u") 'universal-argument)
;; ESC cancels all
(global-set-key (kbd "<escape>") 'keyboard-escape-quit)

;; Required for Evil's `C-r`
(use-package undo-tree
  :config
  (setq undo-tree-auto-save-history nil)
  :init (global-undo-tree-mode 1))

(use-package evil
  :after undo-tree
  :init
  (setq evil-want-C-u-scroll t
        evil-want-integration t
        evil-want-keybinding nil
        evil-want-Y-yank-to-eol t
        evil-split-window-below t
        evil-vsplit-window-right t
        evil-undo-system 'undo-tree)
  :config
  ;; C-g drops to evil's normal mode too
  (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
  ;; M-x easier to press
  (evil-global-set-key 'normal (kbd "C-p") 'execute-extended-command)
  (evil-global-set-key 'normal (kbd "C-c c") 'org-agenda)
  (evil-set-initial-state 'org-agenda-mode 'normal)
  (with-eval-after-load 'evil-maps (define-key evil-motion-state-map (kbd "TAB") nil))

  ;; :q should kill the current buffer rather than quitting Emacs entirely
  (evil-ex-define-cmd "q" 'kill-current-buffer)
  ;; Type :quit to close Emacs
  (evil-ex-define-cmd "quit" 'evil-quit)
  ;; Avoid having to re-type save command
  (evil-ex-define-cmd "W" 'save-buffer)

  (defun fn/dont-arrow-me-bro ()
    (interactive)
    (message "Arrow keys are bad, you know?"))

  ;; Disable arrow keys in normal and visual modes
  (define-key evil-normal-state-map (kbd "<left>") 'fn/dont-arrow-me-bro)
  (define-key evil-normal-state-map (kbd "<right>") 'fn/dont-arrow-me-bro)
  (define-key evil-normal-state-map (kbd "<down>") 'fn/dont-arrow-me-bro)
  (define-key evil-normal-state-map (kbd "<up>") 'fn/dont-arrow-me-bro)
  (evil-global-set-key 'motion (kbd "<left>") 'fn/dont-arrow-me-bro)
  (evil-global-set-key 'motion (kbd "<right>") 'fn/dont-arrow-me-bro)
  (evil-global-set-key 'motion (kbd "<down>") 'fn/dont-arrow-me-bro)
  (evil-global-set-key 'motion (kbd "<up>") 'fn/dont-arrow-me-bro)

  (evil-mode 1))

(use-package evil-collection
  :after evil
  :init (evil-collection-init))
#+end_src

** Org mode
A GNU Emacs major mode for convenient plain text markup — and much more

| Typing the below + TAB | Expands to ...                           |
|------------------------+------------------------------------------|
| <a                     | '#+BEGIN_EXPORT ascii' … '#+END_EXPORT   |
| <c                     | '#+BEGIN_CENTER' …       '#+END_CENTER'  |
| <C                     | '#+BEGIN_COMMENT' …      '#+END_COMMENT' |
| <e                     | '#+BEGIN_EXAMPLE' …      '#+END_EXAMPLE' |
| <E                     | '#+BEGIN_EXPORT' …       '#+END_EXPORT'  |
| <h                     | '#+BEGIN_EXPORT html' …  '#+END_EXPORT'  |
| <l                     | '#+BEGIN_EXPORT latex' … '#+END_EXPORT'  |
| <q                     | '#+BEGIN_QUOTE' …        '#+END_QUOTE'   |
| <s                     | '#+BEGIN_SRC' …          '#+END_SRC'     |
| <v                     | '#+BEGIN_VERSE' …        '#+END_VERSE'   |

#+begin_src emacs-lisp
(use-package org-tempo
  ;; Tell use-package not to try to install org-tempo since it's already there
  :ensure nil)

(use-package org
  ;; Tell use-package not to try to install org since it's already there
  :ensure nil
  :config
  (custom-set-variables
   '(org-directory "~/Documents/org")
   '(org-agenda-files (list org-directory)))
  (setq org-src-tab-acts-natively t)
  (setq org-src-preserve-indentation t))
#+end_src

** Disable Mouse
Disable the mouse in Emacs

#+begin_src emacs-lisp
(use-package disable-mouse
  :init
  (mapc #'disable-mouse-in-keymap
    (list evil-motion-state-map
          evil-normal-state-map
          evil-visual-state-map
          evil-insert-state-map))
  :config (global-disable-mouse-mode 1))
#+end_src

** Theme
An opinionated pack of modern color-themes, a fancy and fast mode-line
inspired by minimalism design and pretty icons. (Doom Emacs theme)

#+begin_src emacs-lisp
(use-package doom-themes
  :config
  ;; If nil, bold/italic is universally disabled
  (setq doom-themes-enable-bold t
        doom-themes-enable-italic t)
  (load-theme 'doom-xcode t))

(use-package doom-modeline
  :after doom-themes
  :init (doom-modeline-mode 1))
#+end_src

** Magit
It's Magit! A Git porcelain inside Emacs

#+begin_src emacs-lisp
(use-package magit)
#+end_src

** Dashboard
An extensible emacs startup screen showing you what’s most important

#+begin_src emacs-lisp
(use-package dashboard
  :init
  (setq dashboard-items '((recents  . 3)
                          (bookmarks . 2)
                          (agenda . 3))
        dashboard-week-agenda t
        dashboard-set-init-info t
        dashboard-center-content t
        dashboard-set-file-icons t
        dashboard-display-icons-p t
        dashboard-set-heading-icons t
        dashboard-icon-type 'nerd-icons
        dashboard-startup-banner (concat user-emacs-directory "banners/ascii.txt")
        dashboard-banner-logo-title "An extensible, customizable, free/libre text editor — and more!")
  :config
  ;; This setting ensures that emacsclient always opens on *dashboard* rather than *scratch*
  (setq initial-buffer-choice (lambda () (get-buffer "*dashboard*")))
  (dashboard-setup-startup-hook))
#+end_src

** LSP

#+begin_src emacs-lisp
(use-package lsp-mode
  :config
  (setq lsp-keymap-prefix "C-c l"
        lsp-modeline-code-actions-enable nil
        lsp-diagnostics-provider :none)
  :hook (python-mode . lsp)
  :commands lsp)

(use-package lsp-jedi
  :after lsp-mode)

(use-package company
  :after lsp-mode
  :hook (after-init . global-company-mode))

(use-package flycheck
  :hook
  (python-mode . (lambda ()
                   (setq flycheck-python-flake8-executable "~/.local/bin/flake8"
                         flycheck-python-pylint-executable "~/.local/bin/pylint")))
  ;; (flycheck-add-next-checker 'python-flake8 'python-pylint)))
  (latex-mode . (lambda ()
                  (add-to-list 'flycheck-disabled-checkers 'tex-chktex)))
  :init (global-flycheck-mode))
#+end_src
